clear;
///////////////////////////////////////////////////////////////////////////////
///////// TESTING SPLIT MODEL DIVISOR ARITHMETIC OVER CHAR TWO FIELDS /////////
///////////////////////////////////////////////////////////////////////////////
//
// Sebastian Lindner, 2020
//-----------------------------------------------------------------------------
// OVERVIEW
//-----------------------------------------------------------------------------
// This script tests divisor arithmetic functions from ch2_splitG2_UTL.mag,
// ch2_splitG2_DBL.mag, and ch2_splitG2_ADD.mag.
//
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
//LOAD FILES
///////////////////////////////////////////////////////////////////////////////
UTL_DEBUG:= false;             
DBL_DEBUG:= false;
ADD_DEBUG:= false;
load "reduced_basis_arithmetic.mag";
load "g2Formulas/ch2_splitG2_UTL.mag";
load "g2Formulas/ch2_splitG2_DBL.mag";
load "g2Formulas/ch2_splitG2_ADD.mag";
"     ";

///////////////////////////////////////////////////////////////////////////////
//GLOBAL TESTING VARIABLES
///////////////////////////////////////////////////////////////////////////////
//Field sizes below 32 for random testing
//Split curves do not exist for FF(2)
FIELDS:= {4,8,16,32};
DIVISORS:= 5000; //Number of divisor additions and doubling to test per curve
TRIALS:= #FIELDS;
DEBUG:= false;
TEST_DBL:= true;
TEST_ADD:= true;


/////////////////////////////////////////////////////////
///////////////////// TEST LOOPS ////////////////////////
/////////////////////////////////////////////////////////

printf "///////////////////////////////////////////////////////////////////////\n";
printf "//\n";
printf "// - Small finite field and curve picked at random for each trial. \n";
printf "// - %o total trials.            \n", TRIALS;
printf "// - %o divisor additions and doubles each per trial.            \n", DIVISORS;
printf "//";
trial:=1;
errorFlag:= false;
for F in FIELDS do
  printf "\n//    Trail # %o over FF(%o).", trial, F;
  trial+:=1;
  // Random finite field FF and polynomial ring over FF
  FF:=GF(F);
  R<x>:=PolynomialRing(FF);
  f,h:= RandomChar2G2SplitCurve(F);
  V:= -ComputeVpl(f,h,2) - h;
  curveConst:= Precompute(f,h,F); 
  

  if DEBUG then
    "\n";
    FF, "f is: ", f, "h is: ", h, "V- is: ", V, "V+ is: ", -V-h;
    " ";
  end if;

  divisor:= 0;
  while divisor le DIVISORS do
    divisor+:=1;

    /////////////////////////////////////////////////////
    ///RANDOM DIVISORS
    /////////////////////////////////////////////////////
    D1:= RandomDivisor_SPLIT_NEG(f,h,V,2);
    U1:= D1[1];
    V1:= D1[2];
    N1:= D1[4];


    D2:= RandomDivisor_SPLIT_NEG(f,h,V,2);
    U2:= D2[1];
    V2:= D2[2];
    N2:= D2[4];


    /////////////////////////////////////////////////////
    ///TEST FORMULAS ON DIVISORS
    /////////////////////////////////////////////////////
    if DEBUG then "  "; end if;
    if TEST_DBL then
      nU2,nU1, nU0, nV1, nV0, nN := DBL(U1,V1,N1,curveConst);
      nU:= R ! nU2*x^2 + nU1*x + nU0;
      nV:= R ! Coeff(V,3)*x^3 + Coeff(V,2)*x^2 + nV1*x + nV0;
      
      cantor:= Double_SPLIT_NEG(D1,f,h,V,2);
      result := <nU,nV,ExactQuotient(f - nV*(nV + h),nU), nN>;
      if result ne cantor then
        printf "Doubling %o failed over finite field FF(%o) where f = %o and h = %o \n", D1, F, f, h;
        errorFlag:= true;
      end if;
    end if;

    if TEST_ADD then
      if D1 ne D2 then
        nU2,nU1, nU0, nV1, nV0, nN := ADD(U1,V1,N1,U2,V2,N2,curveConst);
        nU:= R ! nU2*x^2 + nU1*x + nU0;
        nV:= R ! Coeff(V,3)*x^3 + Coeff(V,2)*x^2 + nV1*x + nV0;
        
        cantor:= Add_SPLIT_NEG(D1,D2,f,h,V,2);
        //cantor;
        result := <nU,nV,ExactQuotient(f - nV*(nV + h),nU), nN>;
        //result;
        if result ne cantor then
          printf "Adding %o with %o failed over finite field FF(%o) where f = %o and h = %o \n", D1, D2, F, f, h;
          errorFlag:= true;
        end if; 
      end if;
    end if;
  end while; 
end for;
printf "\n//";
if errorFlag then
  printf "\n// Errors occured.";
else
  printf "\n// No errors.";
end if;
printf "\n//";
printf "\n///////////////////////////////////////////////////////////////////////\n";
quit;
