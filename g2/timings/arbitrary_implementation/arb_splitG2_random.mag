clear;
///////////////////////////////////////////////////////////////////////////////
/////// TESTING SPLIT MODEL DIVISOR ARITHMETIC OVER ARBITRARY FIELDS //////////
///////////////////////////////////////////////////////////////////////////////
//
// Sebastian Lindner, 2019
//-----------------------------------------------------------------------------
// OVERVIEW
//-----------------------------------------------------------------------------
// This script tests divisor arithmetic functions from arb_splitG2_UTL.mag,
// arb_splitg2_DBL.mag, and arb_splitG2_ADD.mag.
//
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
//LOAD FILES
///////////////////////////////////////////////////////////////////////////////
//UTL_DEBUG:= false;             
//DBL_DEBUG:= false;
//ADD_DEBUG:= false;
load "poly_balanced_arithmetic.mag";
load "g2_utilities.mag";
load "splitFormulas/arb_splitG2_UTL.mag";
load "splitFormulas/arb_splitG2_DBL.mag";
load "splitFormulas/arb_splitG2_ADD.mag";
"     ";

///////////////////////////////////////////////////////////////////////////////
//GLOBAL TESTING VARIABLES
///////////////////////////////////////////////////////////////////////////////
//Field sizes below 20 for random testing
//Split curves do not exist for FF(2)
FIELDS:= {3,4,5,7,8,9,11,13,16,17,19};
DIVISORS:= 1000;
TRIALS:= #FIELDS;
DEBUG:= false;
TEST_DBL:= true;
TEST_ADD:= true;
H_ZERO:= false;


/////////////////////////////////////////////////////////
///////////////////// TEST LOOPS ////////////////////////
/////////////////////////////////////////////////////////

printf "///////////////////////////////////////////////////////////////////////\n";
printf "//\n";
printf "// - Small finite field and curve picked at random for each trial. \n";
printf "// - %o total trials.            \n", TRIALS;
printf "// - %o divisor additions and doubles each per trial.            \n", DIVISORS;
printf "//";
trial:=1;
errorFlag:= false;
for F in FIELDS do
  printf "\n//    Trail # %o over FF(%o).", trial, F;
  trial+:=1;
  // Random finite field FF and polynomial ring over FF
  FF:=GF(F);
  R<x>:=PolynomialRing(FF);

  f,h:= RandomSplitCurve(2,F,H_ZERO);
  V:= ComputeVpl(f,h,2);
  curveConst:= Precompute(f,h,F); 
  

  if DEBUG then
    "\n";
    FF, "f is: ", f, "h is: ", h, "V+ is: ", V, "V- is: ", -V-h;
    " ";
  end if;

  divisor:= 0;
  while divisor le DIVISORS do
    divisor+:=1;

    /////////////////////////////////////////////////////
    ///RANDOM DIVISORS
    /////////////////////////////////////////////////////
    D1:= RandomDivisorAB(f,h,V);
    //Convert to Reduced
    RD1:= ReducedBasis(D1,f,h);
    d1 := < Coefficient(RD1[1],2), Coefficient(RD1[1],1), Coefficient(RD1[1],0), Coefficient(RD1[2],1), Coefficient(RD1[2],0), RD1[3] >;

    D2:= RandomDivisorAB(f,h,V);
    //Convert to Reduced
    RD2:= ReducedBasis(D2,f,h);
    d2 := <Coefficient(RD2[1],2), Coefficient(RD2[1],1),Coefficient(RD2[1],0), Coefficient(RD2[2],1), Coefficient(RD2[2],0),RD2[3]>;



    /////////////////////////////////////////////////////
    ///TEST FORMULAS ON DIVISORS
    /////////////////////////////////////////////////////
    if DEBUG then "  "; end if;
    if TEST_DBL then
      dn := DBL(d1,curveConst);
      dn := <R![dn[3],dn[2],dn[1]], R![dn[5],dn[4],Coeff(V,2),Coeff(V,3)],dn[6]>;
      cantor:= ReducedBasis(Double(D1,f,h,V),f,h);
      if dn ne cantor then
        printf "Doubling %o failed over finite field FF(%o) where f = %o and h = %o \n", RD1, F, f, h;
        errorFlag:= true;
      end if;
    end if;

    if TEST_ADD then
      if D1 ne D2 then
        dn := ADD(d1,d2,curveConst);
        dn := <R![dn[3],dn[2],dn[1]], R![dn[5],dn[4],Coeff(V,2), Coeff(V,3)],dn[6]>;
        cantor:= ReducedBasis(Add(D1,D2,f,h,V),f,h);
        if dn ne cantor then
          printf "Adding %o with %o failed over finite field FF(%o) where f = %o and h = %o \n", RD1, RD2, F, f, h;
          errorFlag:= true;
        end if; 
      end if;
    end if;
  end while; 
end for;
printf "\n//";
if errorFlag then
  printf "\n// Errors occured.";
else
  printf "\n// No errors.";
end if;
printf "\n//";
printf "\n///////////////////////////////////////////////////////////////////////\n";
quit;
