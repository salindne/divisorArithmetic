Deg3ADD_Sutherland := function(u12,u11,u10,v12,v11,v10,u22,u21,u20,v22,v21,v20,f)

    f4:=Coefficient(f,4); 
    f5:=Coefficient(f,5); 
    f6:=Coefficient(f,6);


    t1:=u10-u20; 
    t2:=u11-u21; 
    w0:=u12-u22; 
    t3:=t2-u22*w0;
    t4:=t1-u21*w0; 
    t5:=u22*t3-t4; 
    t6:=u20*w0+u21*t3;
    i0 := t4*t5-t3*t6; 
    i1 := w0*t6-t2*t5; 
    i2 := w0*t4-t2*t3;
    r := t1*i0-u20*(t3*i2+w0*i1);

    t1:=v20-v10; 
    t2:=v11-v21; 
    t3:=v12-v22; 
    t4:=t2*i1; 
    t5:=t1*i0; 
    t6:=t3*i2; 
    t7:=u22*t6;
    t8:=t4+t6+t7-(t2+t3)*(i1+i2); 
    t9:=u20+u22; 
    t10:=(t9+u21)*(t8-t6); 
    t11:=(t9-u21)*(t8+t6);
    q0 := t5-u20*t8;
    q1 := t4-t5+(t11-t10)/2-t7+(t1-t2)*(i0+i1);
    q2 := t6-q0-t4+(t1-t3)*(i0+i2)-(t10+t11)/2;
    t1:=(r+q1)^2 + q2*(r*w0+q2*u21-q1*u22-q0); 
    t2:=2*t1; 
    t3:=r*q2;

    if t2 eq 0 or t3 eq 0 then 
        R<x>:=Parent(f);
        Vn := - ComputeVpl(f,3);
        u := x^3 + u12*x^2 + u11*x + u10;
        v := v12*x^2 + v11*x + v10;
        v := Vn - (Vn - v) mod u;
        w := ExactQuotient(f-v^2,u);
        up := x^3 + u22*x^2 + u21*x + u20;
        vp := v22*x^2 + v21*x + v20;
        vp := Vn - (Vn - vp) mod up;
        wp := ExactQuotient(f-vp^2,up);
        gen := Add_SPLIT_NEG(<u,v,w,0>,<up,vp,wp,0>,f,Vn,3);
        vpp := gen[2] mod gen[1];
        return <Coefficient(gen[1],3), Coefficient(gen[1],2), Coefficient(gen[1],1), Coefficient(gen[1],0), Coefficient(vpp,2), Coefficient(vpp,1), Coefficient(vpp,0) , gen[4]>;
    end if;

    t4 := 1/(t2*t3);  
    t5:=t2*t4;  
    t6:=r*t5;
    w1 := t5*q2^2; 
    w2 := r*t6; 
    w3 := w2^2; 
    w4 := t3^2*t4;
    s0 := t6*q0; 
    s1 := t6*q1; 
    vt43 := t1*t5;

    t6:=s0+s1; 
    t1:=u10+u12; 
    t2:=t6*(t1+u11); 
    t3:=(t1-u11)*(s0-s1); 
    t4:=u12*s1;
    z0:=u10*s0; 
    z1:=(t2-t3)/2-t4; 
    z2:=(t2+t3)/2-z0+u10; 
    z3:=u11+s0+t4; 
    z4:=u12+s1;

    u43 := z4+s1-u22;
    t0:=s1*z4; 
    t1:=u22*u43;
    u42 := z3+t0+s0-w3-u21-t1;
    t2:=u21*u42; 
    t3:=(u21+u22)*(u42+u43)-t1-t2; 
    t4:=2*w2;
    t5:=t4*v12; 
    t6:=s0*z3; 
    t7:=(s0+s1)*(z3+z4)-t0-t6;
    u41 := z2+t7+t5+w3*u12-u20-t3;
    u40 := z1+s1*(t5+z2)+t6+t4*v11-w3*(f6+u12^2-u11)-u20*u43-t2-u22*u41;

    t1:=u43-z4+w2;
    vt40 := v10 + w1*(z0+u40*t1);
    vt41 := v11 + w1*(z1-u40+u41*t1);
    vt42 := v12 + w1*(z2-u41+u42*t1);

    u52 := vt43/2+w4*(2*vt42-f6)-u43;
    u51 := w4*(2*(vt41+vt43*vt42)-f5)-u52*u43-u42;
    u50 := w4*(vt42^2+2*(vt40+vt43*vt41)-f4)-u51*u43-u52*u42-u41;

    t1 := u52 - vt43;
    v50 := vt40 + t1*u50;
    v51 := vt41 - u50 + t1*u51;
    v52 := vt42 - u51 + t1*u52;

    return <1,u52,u51,u50,v52,v51,v50,0>;
end function;

ADD_Sutherland_SPLIT:= function(d1,d2,f)

if IsOne(d1[1]) and IsOne(d2[1]) then 
    return Deg3ADD_Sutherland(d1[2],d1[3],d1[4],d1[5],d1[6],d1[7],d2[2],d2[3],d2[4],d2[5],d2[6],d2[7],f); 
else
    R<x>:=Parent(f);
    Vn := - ComputeVpl(f,3);
    u := d1[1]*x^3 + d1[2]*x^2 + d1[3]*x + d1[4];
    v := d1[5]*x^2 + d1[6]*x + d1[7];
    v := Vn - (Vn - v) mod u;
    w := ExactQuotient(f-v^2,u);
    up := d2[1]*x^3 + d2[2]*x^2 + d2[3]*x + d2[4];
    vp := d2[5]*x^2 + d2[6]*x + d2[7];
    vp := Vn - (Vn - vp) mod up;
    wp := ExactQuotient(f-vp^2,up);
    gen := Add_SPLIT_NEG(<u,v,w,d1[8]>,<up,vp,wp,d2[8]>,f,Vn,3);
    vpp := gen[2] mod gen[1];
    return <Coefficient(gen[1],3), Coefficient(gen[1],2), Coefficient(gen[1],1), Coefficient(gen[1],0), Coefficient(vpp,2), Coefficient(vpp,1), Coefficient(vpp,0) , gen[4]>;
end if;

end function;

   


Deg3DBL_Sutherland := function(u12,u11,u10,v12,v11,v10,f)

    f3:=Coeff(f,3); 
    f4:=Coeff(f,4); 
    f5:=Coeff(f,5); 
    f6:=Coeff(f,6);

    w0:=v11-u12*v12; 
    t2:=v10-u11*v12; 
    t3:=t2-u12*w0; 
    t4:=u10*v12+u11*w0;
    i0:=t2*t3+w0*t4; 
    i1:=-(v11*t3+v12*t4); 
    i2:=v11*w0-v12*t2;
    r:=v10*i0-u10*(w0*i2+v12*i1);

    w1:=u12^2; 
    t2:=2*u10; 
    t3:=3*u11; 
    w2:=w1+f6; 
    t5:=2*t2-f5; 
    t6:=2*u12; 
    t7:=t3-w2;
    p2:=f5+t6*(t7-w1)-t2;
    p1:=u12*t5-v12^2+f4-u11*(2*f6-t3)-w1*(t7+t3);
    p0:=f3-u11*(w1*t6-t5)-t2*w2-u12*p1-2*v11*v12;

    t1:=i1*p1; 
    t2:=i0*p0; 
    t3:=i2*p2; 
    t4:=u12*t3; 
    t5:=(i1+i2)*(p1+p2)-t1-t3-t4; 
    t6:=u10*t5;
    t7:=u10+u12; 
    w3:=t7+u11; 
    t9:=t7-u11; 
    t10:=w3*(t3+t5); 
    t11:=t9*(t5-t3);
    q0:=t2-t6;
    q1:=t4+(i0+i1)*(p0+p1)+(t11-t10)/2-t1-t2;
    q2:=t1+t6+(i0+i2)*(p0+p2)-t2-t3-(t10+t11)/2;

    t0:=2*r; 
    t1:=t0^2; 
    t2:=q2^2;  
    t3:=t1-q0*q2+q1*(2*t0+q1-q2*u12)+t2*u11;
    //38M
    if t0 eq 0 or q2 eq 0 or t3 eq 0 then 
        R<x>:=Parent(f);
        Vn := - ComputeVpl(f,3);
        u := x^3 + u12*x^2 + u11*x + u10;
        v := v12*x^2 + v11*x + v10;
        v := Vn - (Vn - v) mod u;
        w := ExactQuotient(f-v^2,u);
        gen := Double_SPLIT_NEG(<u,v,w,0>,f,Vn,3);
        vpp := gen[2] mod gen[1];
        return <Coefficient(gen[1],3), Coefficient(gen[1],2), Coefficient(gen[1],1), Coefficient(gen[1],0), Coefficient(vpp,2), Coefficient(vpp,1), Coefficient(vpp,0) , gen[4]>;
    end if;

    t4:=1/(t0*q2*t3);  
    w4:=t1*t2*t4;  
    t5:=t3*t4; 
    t6:=t0*t5; 
    w5:=t2*t5; 
    w6:=t1*t5; 
    w7:=w6^2; 
    s0:=t6*q0; 
    s1:=t6*q1;
    vt43:=t3*t5;

    t1:=w3*(s0+s1); 
    t2:=t9*(s0-s1); 
    t3:=u12*s1; 
    z0:=s0*u10;  
    z1:=(t1-t2)/2-t3;  
    z2:=(t1+t2)/2-z0+u10;  
    z3:=u11+s0+t3;  
    z4:=u12+s1;  

    t1:=v12*w6;
    u43 := 2*s1;
    u42 := 2*s0+s1^2-w7;
    u41 := 2*(s0*s1+u12*w7+t1);
    u40 := s0^2+2*(w0*w6+s1*t1)-w7*(w2+2*(w1-u11));

    w7:=u43-z4+w6;
    vt40 := v10 + w5*(z0+u40*w7);
    vt41 := v11 + w5*(z1-u40+u41*w7);
    vt42 := v12 + w5*(z2-u41+u42*w7);

    u52 := vt43/2+w4*(vt42-f6/2)-u43;
    u51 := vt42+w4*(vt41-f5/2)-u52*u43-u42;
    u50 := vt41+w4*((vt42^2-f4)/2+vt40)-u51*u43-u52*u42-u41;

    t1:=u52-vt43;
    v50 := vt40 + t1*u50;
    v51 := vt41 - u50 + t1*u51;
    v52 := vt42 - u51 + t1*u52;


    return <1,u52,u51,u50,v52,v51,v50,0>;
end function;

DBL_Sutherland_SPLIT:= function(d,f)
    if IsOne(d[1]) then 
        return Deg3DBL_Sutherland(d[2],d[3],d[4],d[5],d[6],d[7],f); 
    else
        R<x>:=Parent(f);
        Vn := - ComputeVpl(f,3);
        u := d[1]*x^3 + d[2]*x^2 + d[3]*x + d[4];
        v := d[5]*x^2 + d[6]*x + d[7];
        v := Vn - (Vn - v) mod u;
        w := ExactQuotient(f-v^2,u);
        gen := Double_SPLIT_NEG(<u,v,w,d[8]>,f,Vn,3);
        vpp := gen[2] mod gen[1];
        return <Coefficient(gen[1],3), Coefficient(gen[1],2), Coefficient(gen[1],1), Coefficient(gen[1],0), Coefficient(vpp,2), Coefficient(vpp,1), Coefficient(vpp,0) , gen[4]>;
    end if;
end function;